apply plugin: 'com.android.application'
if (file('fabric.properties').exists()) {
    apply plugin: 'io.fabric'
}

android {
    compileSdkVersion compileAndTargetSdkLevel
    buildToolsVersion buildTools

    defaultConfig {
        minSdkVersion minSdkLevel
        targetSdkVersion compileAndTargetSdkLevel
        versionCode versionMajor * 1000000 + versionMinor * 10000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        resValue 'string', 'api_endpoint', apiProps["endpoint"] != null ? apiProps["endpoint"] : ''
        resValue 'string', 'api_key', apiProps["key"] != null ? apiProps["key"] : ''

        resConfigs "en"
    }

    signingConfigs {
        release {
            storeFile keyProps["store"] != null ? file(keyProps["store"]) : null
            keyAlias keyProps["alias"] ?: ""
            storePassword keyProps["storePass"] ?: ""
            keyPassword keyProps["pass"] ?: ""
        }
    }

    buildTypes {
        debug {
            versionNameSuffix '_debug_' + gitSha()
            applicationIdSuffix '.debug'
            ext.enableCrashlytics = false
        }
        release {
            if (keyProps["store"] != null) {
                signingConfig signingConfigs.release
            }
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {

    // Muzei
    compile "com.google.android.apps.muzei:muzei-api:2.0"

    // Dagger 2
    compile "com.google.dagger:dagger:${daggerVersion}"
    annotationProcessor "com.google.dagger:dagger-compiler:${daggerVersion}"
    provided "javax.annotation:jsr250-api:1.0"

    // Support
    compile "com.android.support:appcompat-v7:${supportLibraryVersion}"

    // Network
    compile "com.squareup.retrofit2:retrofit:2.1.0"
    compile "com.squareup.retrofit2:converter-moshi:2.1.0"
    compile "com.squareup.okhttp3:okhttp:3.4.1"
    compile 'com.squareup.okhttp3:logging-interceptor:3.4.1'
    compile 'com.squareup.moshi:moshi:1.2.0'

    // Other
    compile "com.jakewharton.timber:timber:4.3.0"
    compile('de.psdev.licensesdialog:licensesdialog:1.8.1') {
        exclude module: 'support-v4'
    }
    compile('com.crashlytics.sdk.android:crashlytics:2.6.3@aar') {
        transitive = true;
    }

    // Test
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'org.powermock:powermock-module-junit4:1.6.5'
    testCompile 'org.powermock:powermock-api-mockito:1.6.5'
}

apply from: '../config/static-analysis.gradle'
apply from: '../config/test-coverage.gradle'
