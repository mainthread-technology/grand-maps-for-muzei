/**
 *  From:
 *  https://blog.gouline.net/2015/06/23/code-coverage-on-android-with-jacoco/
 *  & https://github.com/mgouline/android-samples/tree/master/jacoco
 */
apply plugin: 'jacoco'

jacoco {
    toolVersion = "0.7.5.201505241946"
}

project.afterEvaluate {
    // Grab all build types and product flavors
    def buildTypes = android.buildTypes.collect { type -> type.name }
    def productFlavors = android.productFlavors.collect { flavor -> flavor.name }

    // When no product flavors defined, use empty
    if (!productFlavors) productFlavors.add('')

    productFlavors.each { productFlavorName ->
        buildTypes.each { buildTypeName ->
            def sourceName = "${buildTypeName}"
            if (productFlavorName) {
                sourceName = "${productFlavorName}${sourceName.capitalize()}"
            }
            def targetName = "${sourceName.capitalize()}"
            def testTaskName = "test${sourceName.capitalize()}UnitTest"
            def coverageTaskName = "${testTaskName}Coverage"

            // Create coverage task of form 'testFlavorTypeCoverage' depending on 'testFlavorTypeUnitTest'
            //noinspection GroovyAssignabilityCheck
            task "$coverageTaskName"(type: JacocoReport, dependsOn: "$testTaskName") {
                group = "Reporting"
                description = "Generate Jacoco coverage reports on the ${targetName} build."

                classDirectories = fileTree(
                        dir: "${project.buildDir}/intermediates/classes/${sourceName}",
                        excludes: ['**/R.class',
                                   'technology/mainthread/apps/grandmaps/injector/**',
                                   '**/R$*.class',
                                   '**/BuildConfig.*',
                                   '**/Manifest*.*',]
                )

                def coverageSourceDirs = [
                        "src/main/java",
                        "src/$buildTypeName/java"
                ]
                additionalSourceDirs = files(coverageSourceDirs)
                sourceDirectories = files(coverageSourceDirs)
                executionData = files("${project.buildDir}/jacoco/${testTaskName}.exec")

                reports {
                    xml.enabled = false
                    html.enabled = true
                }
            }
        }
    }
}