apply plugin: 'checkstyle'
apply plugin: 'pmd'

// comment this line to remove the static analysis tasks from the build task
check.dependsOn 'checkstyle', 'pmd'

task checkstyle(type: Checkstyle) {
    description 'Checks if the code passes quality standards'
    group 'verification'

    configFile file("${project(':').projectDir}/config/checkstyle/checkstyle.xml")
    source 'src'
    include '**/*.java'
    classpath = files()

    ignoreFailures false
    showViolations true
}

task pmd(type: Pmd) {
    description 'PMD scans Java source code and looks for potential problems'
    group 'verification'

    source 'src'
    include '**/*.java'
    exclude '**/gen/**'

    reports {
        xml.enabled = false
        html.enabled = true
    }
}
