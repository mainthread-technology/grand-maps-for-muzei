// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.5.0-beta2'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
        //noinspection GradleDynamicVersion
        classpath 'io.fabric.tools:gradle:1.+'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.11.3'
    }
}

allprojects {
    repositories {
        jcenter()
        maven {
            url 'https://maven.fabric.io/public'
        }
    }
}

apply plugin: 'com.github.ben-manes.versions'

project.ext.preDexLibs = !project.hasProperty('disablePreDex')

subprojects {
    project.plugins.whenPluginAdded { plugin ->
        if ("com.android.build.gradle.AppPlugin".equals(plugin.class.name)) {
            project.android.dexOptions.preDexLibraries = rootProject.ext.preDexLibs
        } else if ("com.android.build.gradle.LibraryPlugin".equals(plugin.class.name)) {
            project.android.dexOptions.preDexLibraries = rootProject.ext.preDexLibs
        }
    }

    ext {
        gitSha = { ->
            try {
                def stdout = new ByteArrayOutputStream()
                exec {
                    commandLine 'git', 'rev-parse', '--short', 'HEAD'
                    standardOutput = stdout
                }
                return stdout.toString().trim()
            } catch (ignored) {
                return null;
            }
        }

        def Properties localProps = new Properties()
        File localPropsFile = file('../local.properties')
        if (localPropsFile.exists()) {
            localProps.load(new FileInputStream(localPropsFile))
        }
        keyProps = new Properties()
        if (localProps['keystore.props.file'] != null) {
            keyProps.load(new FileInputStream(file(localProps['keystore.props.file'])))
        }

        versionMajor = 1
        versionMinor = 13
        versionPatch = 0
        versionBuild = 0 // bump for dogfood builds, public betas, etc.

        buildTools = "23.0.2"
        compileAndTargetSdkLevel = 23
        minSdkLevel = 17
        commonSupportLibraryVersion = '23.1.0'
        daggerVersion = '2.0.2'
    }
}